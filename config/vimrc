"
" vim settings by Joakim Tufvegren
"

" Basic settings
set background=dark ignorecase smartcase tabstop=4 smartindent backspace=2 ruler shiftwidth=4
syntax on

" Omni-completion and youcompleteme
filetype plugin on
set omnifunc=syntaxcomplete#Complete
set completeopt=menuone
" Use SQL omnicomplete without dbext by default
let g:omni_sql_default_compl_type = 'syntax'
" Switch to a more C friendly default config for Clang completion
let g:ycm_global_ycm_extra_conf = '~/.vim/plugin/ycm_extra_conf.py'
let g:ycm_autoclose_preview_window_after_completion = 1
let g:ycm_autoclose_preview_window_after_insertion = 1

" Fix gvim background color
if has('gui_running')
  highlight Normal guifg=white guibg=black
endif

" Highlight trailing whitespace and space before tab
highlight ExtraWhitespace ctermbg=darkred guibg=darkred
autocmd ColorScheme * highlight ExtraWhitespace ctermbg=darkred guibg=darkred
autocmd BufWinEnter * match ExtraWhitespace /\(\s\+$\| \+\ze\t\)/
autocmd InsertEnter * match ExtraWhitespace /\(\s\+\%#\@<!$\| \+\ze\t\)/
autocmd InsertLeave * match ExtraWhitespace /\(\s\+$\| \+\ze\t\)/
if version >= 702
  autocmd BufWinLeave * call clearmatches()
endif

" Add command to fix trailing whitespace
function! <SID>StripTrailingWhitespace()
    " Preparation: save last search, and cursor position.
    let _s=@/
    let l = line(".")
    let c = col(".")
    " Do the business:
    %s/\s\+$//e
    " Clean up: restore previous search history, and cursor position
    let @/=_s
    call cursor(l, c)
endfunction
command StripTWS call <SID>StripTrailingWhitespace()

" Add w!! to write with sudo privileges
command SudoWrite %!sudo tee > /dev/null %
cabbrev w!! <c-r>=(getcmdpos()==1 && getcmdtype()==':' ? 'SudoWrite' : 'w!!')<CR>

" Disable smartindent/autoindent for git commit messages
autocmd FileType gitcommit setlocal noautoindent nosmartindent
